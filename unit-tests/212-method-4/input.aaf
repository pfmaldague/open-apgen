apgen version "faster"

resource R: consumable integer
    begin
	parameters
	    x: integer default to 0;
	profile
	    0;
	usage
	    -x;
    end resource R

modeling function use_R(y)
    parameters
	y: integer default to 0;
    {
    use R(y);
    wait for 5:00;
    use R(-y);
    }

function compute_duration(y, P)
    parameters
	y: integer default to 0;
	P: instance default to "generic";
    {
    return P.parent.span / 2;
    }

activity type the_grandchild
    begin
	attributes
		"Color" = "Hot Pink";
		"Legend" = "grandchildren";
		"Duration" = 00:30:00;
	parameters
		x: integer default to 0;
	modeling
		write_to_stdout(now, " - id: ", id, "\n");
		use_R(x);
    end activity type the_grandchild

nonexclusive_decomposition function create_grandchild(a, b)
    parameters
	a: integer default to 0;
	b: duration default to 0:0:0;
    {
    write_to_stdout(now, " - create_grandchild(", a, ", ", b, ")\n");
    the_grandchild(a) at now + b;
    }

activity type the_child
    begin
	x: integer default to 13;
	attributes
		"Color" = "Purple";
		"Description" = "a child";
		"Duration" = 01:00:00;
		"Legend" = "Children";
	parameters
		delay: duration default to 30:00;
		intensity: integer default to 5;
	creation
		x = 10;
	nonexclusive_decomposition
		create_grandchild(intensity, delay);
	modeling
		estimate_1: duration default to parent.span / 2;
		estimate_2: duration default to compute_duration(x, this);
		estimate_3: duration default to get_parent_of(this).span / 2;
		span = estimate_2;
		write_to_stdout("the_child: local var x = ", x, "; estimates: ",
			estimate_1, ", ",
			estimate_2, ", ",
			estimate_3, ", actual: ",
			span,
			"\n");
    end activity type the_child

activity type the_parent
    begin
	attributes
		"Color" = "Lavender";
		"Duration" = 1:00:00;
		"Legend" = "Parents";
	parameters
		C: integer default to 2 ? "Number of children";
	nonexclusive_decomposition
		i: integer default to 0;
		j: integer default to 0;
		T: time default to start;
		A: array default to [5, 8, 2, 11, 24, 0, 9, 7];
		while(i < C) {
			write_to_stdout(id, ": creating child at ", T, "\n");
			the_child(25:00 * i, A[j]) at T;
			T = T + span;
			i = i + 1;
			j = j + 1;
			if(j >= length_of(A)) {
				j = 0;
			}
		}
    end activity type the_parent

activity instance Foo of type the_parent id generic
    begin
	attributes
	    "Color" = "Yellow";
	    "Description" = "Foo (id )";
	    "Duration" = 01:00:00;
	    "Legend" = "Parents";
	    "Pattern" = 0;
	    "Plan" = "59-read-apf/input.aaf";
	    "Start" = 2028-163T01:02:03.000;
	    "Status" = TRUE;
	parameters
	    (8);
    end activity instance Foo

